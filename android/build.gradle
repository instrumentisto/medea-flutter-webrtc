group = "com.instrumentisto.medea_flutter_webrtc"
version = "0.16.2-dev"

def webrtcBranch = System.getenv("WEBRTC_BRANCH")

buildscript {
    ext.kotlin_version = "2.2.20"
    repositories {
        google()
        mavenCentral()
    }

    dependencies {
        classpath("com.android.tools.build:gradle:8.11.1")
        classpath("org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version")
    }
}

rootProject.allprojects {
    repositories {
        google()
        mavenCentral()

        if (webrtcBranch == null) {
            maven {
                url "https://instrumentisto.github.io/libwebrtc-bin/android"
            }
        } else {
            flatDir { dirs "lib" }
        }
    }
}

apply plugin: "com.android.library"
apply plugin: "kotlin-android"

android {
    namespace = "com.instrumentisto.medea_flutter_webrtc"

    compileSdk = 36

    compileOptions {
        sourceCompatibility = JavaVersion.VERSION_17
        targetCompatibility = JavaVersion.VERSION_17
    }

    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_17
    }

    sourceSets {
        main.java.srcDirs += "src/main/kotlin"
    }

    defaultConfig {
        minSdk = 24
        consumerProguardFiles("proguard-rules.pro")
    }

    buildFeatures {
        buildConfig = true
    }

    dependencies {
        if (webrtcBranch == null) {
            implementation "com.github.instrumentisto:libwebrtc-bin:143.0.7499.169@aar"
        } else {
            implementation(name: "libwebrtc-bin-${webrtcBranch}", ext: 'aar')
        }
    }
}
